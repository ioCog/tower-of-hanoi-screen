////ioCog Rules File
////Tower of Hanoi
////Rob Lewis
////4 Aug 2017

Rule Strategy(
	Comment "Breaks Goals down into simpler goals for moving stack";
	Goal (Stack {<stack> Length > 1}, From <from>, To <to>);
	Peg (PegNumber {<temp> <> <from> <> <to>});
-->
	Set <disk> = Pop(<stack>);
	Make Goal (Stack <stack>, From <temp>, To <to>);
	Make Goal (Stack <disk>, From <from>, To <to>);
	Make Goal (Stack <stack>, From <from>, To <temp>);
	Remove 1;
);



Rule MoveDisk(
	Comment "Moves a topmost disk to top of pile on target peg";
	Goal (Stack <disk>, From <from>, To <to>);
	Disk (Size = <disk>, Peg = <from>);
	! Disk (Peg = <from>, Size < <disk>);
	! Disk (Peg = <to>, Size < <disk>);
-->
	Modify 2 (Peg <to> );
	Remove 1;
);

